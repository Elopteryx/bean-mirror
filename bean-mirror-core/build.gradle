ext.moduleName = 'com.github.elopteryx.reflect'

compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'bean-mirror'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Bean Mirror'
                groupId = 'com.github.elopteryx'
                artifactId = 'bean-mirror'

                description = 'Bean Mirror is a reflection library.'
                url = 'https://github.com/Elopteryx/bean-mirror'

                scm {
                    connection = 'scm:git:git@github.com/Elopteryx/bean-mirror.git'
                    developerConnection = 'scm:git:git@github.com/Elopteryx/bean-mirror.git'
                    url = 'scm:git:git@github.com/Elopteryx/bean-mirror.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'elopteryx'
                        name = 'Adam Forgacs'
                        email = 'creative.elopteryx@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
