plugins {
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'com.github.spotbugs' version '5.0.14'
}

allprojects {
    apply plugin: 'jacoco'
    jacoco {
        toolVersion = '0.8.10'
    }
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'

    group = 'com.github.elopteryx'
    version = '2.1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        jmhVersion = '1.36'
        junitVersion = '5.9.2'

        checkStyleVersion = '10.12.1'
        pmdVersion = '6.55.0'
        spotbugsVersion = '4.7.3'
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    java {
        withJavadocJar()
        withSourcesJar()
        modularity.inferModulePath = true
    }

    javadoc {
        afterEvaluate {
            configure(options) {
                windowTitle = 'Bean Mirror API Documentation'
                docTitle = 'Bean Mirror API Documentation'
                bottom = 'Copyright &#169; 2021 <a href="https://github.com/Elopteryx">Creative Elopteryx</a>'
                breakIterator = true
                author = false
                source = '17'
                encoding = 'UTF-8'
                docEncoding = 'UTF-8'
                failOnError = true
                links = [
                        'https://docs.oracle.com/en/java/javase/17/docs/api/'
                ]
                modulePath = configurations.compileClasspath.asList()
            }
        }
    }

    checkstyle {
        toolVersion = checkStyleVersion
        configFile = file("${projectDir}/../config/checkstyle/checkstyle.xml")
    }

    pmd {
        toolVersion = pmdVersion
        incrementalAnalysis = true
        ruleSets = []
        ruleSetConfig = resources.text.fromFile(file("${rootDir}/config/pmd/rulesSets.xml"))
    }

    spotbugs {
        toolVersion = spotbugsVersion
        effort = 'max'
    }

    spotbugsTest {
        enabled = false
    }

    signing {
        required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask('publish') }
        sign configurations.archives
    }

    test {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
    }
}

repositories {
    mavenCentral()
}
